!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the initial buoyancy computation
!>
module initial_buoyancy_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use yaxt,                       only : xt_initialize, xt_finalize
  use mpi_mod,                    only : store_comm, clear_comm
  use pFUnit_Mod

  implicit none

  private
  public :: initial_buoyancy_kernel_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: initial_buoyancy_kernel_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type initial_buoyancy_kernel_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity = 9.80665_r_def
  real(r_def), parameter :: radius  = 6371229.0_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 287.05_r_def
  real(r_def), parameter :: cp      = 1005.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : geometry_planar,    &
                                          partitioner_planar, &
                                          topology_fully_periodic
    use feign_config_mod,          only : feign_base_mesh_config,           &
                                          feign_idealised_config,           &
                                          feign_initial_density_config,     &
                                          feign_initial_temperature_config, &
                                          feign_planet_config
    use idealised_config_mod,      only : test_gravity_wave

    implicit none

    class(initial_buoyancy_kernel_test_type), intent(inout) :: this

    integer :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_base_mesh_config( filename='foo',                      &
                                 prime_mesh_name='unit_test',         &
                                 geometry=geometry_planar,            &
                                 partitioner=partitioner_planar,      &
                                 topology=topology_fully_periodic,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_idealised_config( test=test_gravity_wave )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod,        only: final_configuration

    implicit none

    class(initial_buoyancy_kernel_test_type), intent(inout) :: this

    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use initial_buoyancy_kernel_mod, only : initial_buoyancy_code

    use get_unit_test_m3x3_q3x3x3_sizes_mod, only: get_w0_m3x3_q3x3x3_size, &
                                                   get_wtheta_m3x3_q3x3x3_size

    use get_unit_test_m3x3_dofmap_mod, only: get_w0_m3x3_dofmap, &
                                             get_wtheta_m3x3_dofmap

    use get_unit_test_3x3x3_chi_mod, only: get_w0_3x3x3_field

    implicit none

    class(initial_buoyancy_kernel_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 2000.0_r_def, &
                              dz = 2000.0_r_def

    real(r_def) :: answer
    integer(i_def) :: nlayers, cell

    integer(i_def) :: ndf_w0, ndf_wt
    integer(i_def) :: undf_w0, undf_wt

    ! Dummy variable for passing into multi-getter routines where item is not
    ! needed
    integer(i_def) :: unused

    integer(i_def), allocatable :: map_w0(:,:)
    integer(i_def), allocatable :: map_wt(:,:)

    real(r_def), allocatable :: chi_data(:,:)
    real(r_def), allocatable :: buoyancy_data(:)

    ! Unit test doesn't use the 3x3 basis - so declare  a specific one here
    real(r_def) :: basis_w0(16) = [0.25,0.25,0.25,0.25,0.0,0.0,0.0,0.0, &
                                   0.0,0.0,0.0,0.0,0.25,0.25,0.25,0.25]

    nlayers=3

    ! Get sizes
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, unused, &
                                  unused, unused,          &
                                  unused, unused,          &
                                  nlayers )

    call get_wtheta_m3x3_q3x3x3_size( ndf_wt, undf_wt, unused, &
                                      unused, unused,          &
                                      unused, unused,          &
                                      nlayers )

    ! Get dofmaps
    call get_w0_m3x3_dofmap(map_w0)
    call get_wtheta_m3x3_dofmap(map_wt)

    ! Create the data arrays
    allocate(chi_data(undf_w0,3))
    allocate(buoyancy_data(undf_wt))

    call get_w0_3x3x3_field(chi_data(:,1), chi_data(:,2), chi_data(:,3), &
                            dx, dy, dz, map_w0, nlayers)
    cell = 1

    call initial_buoyancy_code( nlayers,                 &
                                buoyancy_data,           &
                                chi_data(:,1),           &
                                chi_data(:,2),           &
                                chi_data(:,3),           &
                                ndf_wt, undf_wt,         &
                                map_wt(:,cell),          &
                                ndf_w0, undf_w0,         &
                                map_w0(:,cell), basis_w0 &
                              )

    answer = 0.6812726E-03_r_def
    @assertEqual(answer, buoyancy_data(3), tol)

    deallocate(map_w0)
    deallocate(map_wt)
    deallocate(chi_data)
    deallocate(buoyancy_data)

  end subroutine test_all

end module initial_buoyancy_kernel_mod_test
