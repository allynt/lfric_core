!-----------------------------------------------------------------------------
! (C) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Integrate a W3 field as though it were a finite difference
!>        representation
module calc_total_mass_alg_mod

  implicit none

  private
  public :: calc_total_mass_alg

contains

  subroutine calc_total_mass_alg( total_mass, rho, mesh, twod_mesh )

    use compute_column_integral_kernel_mod,                                       &
                                        only: compute_column_integral_kernel_type
    use constants_mod,                  only: r_def, i_def
    use fs_continuity_mod,              only: W3, Wtheta
    use field_mod,                      only: field_type
    use function_space_collection_mod,  only: function_space_collection
    use geometric_constants_mod,        only: get_height,   &
                                              get_panel_id
    use mesh_mod,                       only: mesh_type
    use physical_op_constants_mod,      only: get_da_msl_proj
    use planet_config_mod,              only: radius

    implicit none

    type( field_type ), intent(in)         :: rho
    type( mesh_type ), intent(in), pointer :: mesh
    type( mesh_type ), intent(in), pointer :: twod_mesh
    real( kind=r_def ), intent(out)        :: total_mass

    type( field_type ), pointer :: height_w3 => null()
    type( field_type ), pointer :: height_wth => null()
    type( field_type ), pointer :: dA => null()
    type( field_type )          :: tot_col_mass

    integer( kind=i_def ) :: mesh_id

    mesh_id    =  mesh%get_id()
    height_w3  => get_height( W3, mesh_id )
    height_wth => get_height( Wtheta, mesh_id )
    dA         => get_da_msl_proj( twod_mesh%get_id() )

    call tot_col_mass%initialise( vector_space = &
                                  function_space_collection%get_fs(twod_mesh, 0, W3) )

    total_mass = 0.0_r_def

    call invoke( compute_column_integral_kernel_type(rho, height_w3, height_wth, &
                                            tot_col_mass, radius),               &
                 inc_X_times_Y(tot_col_mass, dA),                                &
                 sum_X(total_mass, tot_col_mass) )

    nullify( height_w3, height_wth, dA )

  end subroutine calc_total_mass_alg

end module calc_total_mass_alg_mod
